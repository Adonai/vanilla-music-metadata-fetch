apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.kanedias.vanilla.metadata"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('misc/signing.keystore')
            storePassword System.getenv('SIGNING_KEYSTORE_PASSWORD')
            keyAlias 'release-key'
            keyPassword System.getenv('RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {}

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def versionCodes = ["universal": 0, "armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4]
            int abiVersionCode = versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI) ?: "universal")
            output.versionCodeOverride = abiVersionCode + android.defaultConfig.versionCode * 10
        }
    }
}

task traceCommitsToReleaseNotes(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File('src/main/play/release-notes/en-US/default.txt')
    fromRef = "1.0.0"
    toRef = android.defaultConfig.versionName
    templateContent = """
        {{#commits}}
        - {{{message}}}
        {{/commits}}
    """.stripMargin()
}
publish.dependsOn traceCommitsToReleaseNotes


play {
    serviceAccountCredentials = file("misc/android-publisher-account.json")
    track = 'production'
}

dependencies {
    implementation project(':plugin-commons') // SAF storage access utilities
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"     // Kotlin language support
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"        // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0"     // Kotlin UI context
    implementation 'com.github.QuickLyric:fpcalc-android:1.0.2b'                 // convert audiofile to fingerprint
    implementation 'com.google.code.gson:gson:2.8.5'                            //
}

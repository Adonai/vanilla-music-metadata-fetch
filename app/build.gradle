plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'com.github.triplet.play' version '2.4.1'
}

import static org.apache.tools.ant.taskdefs.condition.Os.*

android {
    ndkVersion "21.3.6528147"
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.kanedias.vanilla.metadata"
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 9
        versionName "1.0.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('misc/signing.keystore')
            storePassword System.getenv('SIGNING_KEYSTORE_PASSWORD')
            keyAlias 'release-key'
            keyPassword System.getenv('RELEASE_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            debuggable = true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk false
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def versionCodes = ["universal": 0, "armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4]
            int abiVersionCode = versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI) ?: "universal")
            output.versionCodeOverride = abiVersionCode + android.defaultConfig.versionCode * 10
        }
    }
}

task populateChangelog {
    doLast {
        def prevTag = ['git',  'describe', '--tags', '--abbrev=0', 'HEAD~'].execute()
        prevTag.waitFor()
        def log = ['git', 'log', '--format=- %s (%an)', "${prevTag.in.readLines().first()}..HEAD"].execute()
        log.waitFor()

        def cleanup = ['git', 'checkout', 'app/src/main/play/release-notes'].execute()
        cleanup.waitFor()
        println cleanup.err.text

        def changelog = file("src/main/play/release-notes/en-US/default.txt")
        changelog.text += "\n" + log.in.text
    }
}

if (isFamily(FAMILY_UNIX)) {
    publish.dependsOn populateChangelog
}


play {
    serviceAccountCredentials = file("misc/android-publisher-account.json")
    track = 'production'
}

dependencies {
    implementation 'com.github.vanilla-music:vanilla-music-plugin-commons:1.0.3' // SAF storage access utilities
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"      // Kotlin language support
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"         // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.4"      // Kotlin UI context
    implementation 'androidx.core:core:1.3.0'                                    // Android FileProvider
    implementation 'com.github.QuickLyric:fpcalc-android:1.0.2b'                 // convert audiofile to fingerprint
    implementation 'com.google.code.gson:gson:2.8.6'                             // Google JSON ORM library
}
